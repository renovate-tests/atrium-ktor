buildscript {
    project.group = "com.tegonal.atrium.ktor"
    project.version = "0.1.0-SNAPSHOT"
    project.description = "Atrium assertion functions for Ktor types"

    ext {
        // main
        kotlin_version = '1.3.31'
        atrium_version = '0.8.0'
        ktor_version = '1.2.0'

        //test
        detekt_version = '1.0.0-RC14'
    }

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
        jcenter()
    }

    def tutteli_gradle_version = gradle.ext.tutteli_plugins_version
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath("ch.tutteli:tutteli-gradle-kotlin-module-info:$gradle.ext.tutteli_plugins_version")
        classpath "ch.tutteli:tutteli-gradle-dokka:$tutteli_gradle_version"
        classpath "ch.tutteli:tutteli-gradle-kotlin-utils:$tutteli_gradle_version"
        classpath "ch.tutteli:tutteli-gradle-publish:$tutteli_gradle_version"
        classpath "ch.tutteli:tutteli-gradle-spek:$tutteli_gradle_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: "io.gitlab.arturbosch.detekt"
detekt {
    failFast = true
    input = files(rootProject.projectDir)
    filters = ".*/resources/.*,.*$rootProject.name/.*/build/.*"
    config = files("$rootProject.projectDir/gradle/detekt.yml")
    reports {
        xml.enabled = true
    }
}


subprojects {

    apply plugin: 'kotlin'
    apply plugin: 'ch.tutteli.kotlin.utils'
    kotlinutils.kotlinVersion = kotlin_version

    apply plugin: 'ch.tutteli.dokka'
    tutteliDokka.githubUser = 'tegonal'

    apply plugin: 'ch.tutteli.spek'

    apply plugin: 'ch.tutteli.publish'
    tutteliPublish {
        githubUser = 'tegonal'
        bintrayOrganisation = 'tegonal'
        bintrayRepo = 'oss'
        manifestVendor = 'Tegonal GmbH'
        resetLicenses 'EUPL-1.2'
    }

    dependencies {
        implementation kotlinStdlib()
        implementation kotlinReflect()
        implementation "ch.tutteli.atrium:atrium-cc-en_GB-robstoll:$atrium_version", excludeKotlin
    }
}
